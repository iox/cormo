<!DOCTYPE html><html lang="en"><head><meta charset="utf-8"><meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no"><title>CORMO - model.coffee</title><link href="../bootstrap/css/bootstrap.min.css" rel="stylesheet" type="text/css"><link href="../bootstrap/css/bootstrap-responsive.min.css" rel="stylesheet" type="text/css"><link href="../google-code-prettify/prettify.css" rel="stylesheet" type="text/css"><link href="../style.css" rel="stylesheet" type="text/css"></head><body data-spy="scroll" data-target=".sidebar"><div class="navbar navbar-inverse navbar-fixed-top"><div class="navbar-inner"><div class="container-fluid"><ul class="nav"><li class=""><a href="../index.html">Home</a></li><li class="dropdown "><a data-toggle="dropdown" href="#" class="dropdown-toggle">Guides <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../guides/CLI.html">CLI</a></li></ul></li><li class=""><a href="../modules/cormo.html">Modules</a></li><li class=""><a href="../classes/ColumnProperty.html">Classes</a></li><li class="dropdown active"><a data-toggle="dropdown" href="#" class="dropdown-toggle">Files - model.coffee <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../files/command.coffee.html">command.coffee</a></li><li><a href="../files/connection.coffee.html">connection.coffee</a></li><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/inflector.coffee.html">inflector.coffee</a></li><li><a href="../files/model.coffee.html">model.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li><li><a href="../files/util.coffee.html">util.coffee</a></li><li><a href="../files/adapters.base.coffee.html">adapters.base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">adapters.mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">adapters.mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">adapters.postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">adapters.redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">adapters.sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">adapters.sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">adapters.sqlite3_memory.coffee</a></li><li><a href="../files/command.console.coffee.html">command.console.coffee</a></li><li><a href="../files/connection.association.coffee.html">connection.association.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">connection.manipulate.coffee</a></li><li><a href="../files/model.cache.coffee.html">model.cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">model.callback.coffee</a></li><li><a href="../files/model.persistence.coffee.html">model.persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">model.query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">model.timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">model.validate.coffee</a></li></ul></li></ul><div class="options"><label class="checkbox"><input id="options-private" type="checkbox"> Private </label></div></div></div></div><div class="container-fluid content"><div class="row-fluid"><div class="span3 visible-desktop"><div data-spy="affix" class="span3 sidebar"><ul class="nav nav-list cormo-sidenav"><li><a href="../files/command.coffee.html">command.coffee</a></li><li><a href="../files/connection.coffee.html">connection.coffee</a></li><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/inflector.coffee.html">inflector.coffee</a></li><li class="active"><a href="../files/model.coffee.html">model.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li><li><a href="../files/util.coffee.html">util.coffee</a></li><li class="nav-header">adapters</li><li><a href="../files/adapters.base.coffee.html">base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">sqlite3_memory.coffee</a></li><li class="nav-header">command</li><li><a href="../files/command.console.coffee.html">console.coffee</a></li><li class="nav-header">connection</li><li><a href="../files/connection.association.coffee.html">association.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">manipulate.coffee</a></li><li class="nav-header">model</li><li><a href="../files/model.cache.coffee.html">cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">callback.coffee</a></li><li><a href="../files/model.persistence.coffee.html">persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">validate.coffee</a></li></ul></div></div><div class="span9"><section><h1>model.coffee</h1></section><pre class="prettyprint">_ = require 'underscore'
types = require './types'
util = require './util'

_bindDomain = (fn) -&gt; if d = process.domain then d.bind fn else fn

##
# Properties of a column of a model
class ColumnProperty
  ##
  # @property type

  ##
  # @property required

  ##
  # @property unique

  ##
  # @property _parts
  # @private

  ##
  # Name for SQL dbs.
  # e.g.) name.first -&gt; name_first
  # @property _dbname
  # @private

##
# Base class for models
# @uses ModelQuery
# @uses ModelCallback
# @uses ModelTimestamp
# @uses ModelPersistence
# @uses ModelValidate
# @uses ModelCache
class Model
  ##
  # Tracks changes of a record if true
  # @type Boolean
  @dirty_tracking: false

  @eliminate_null: false

  ##
  # Archives deleted records in the archive table
  @archive: false

  ##
  # Indicates the connection associated to this model
  # @property _connection
  # @type Connection
  # @private
  # @static
  # @see Model.connection

  ##
  # Indicates the adapter associated to this model
  # @property _adapter
  # @type AdapterBase
  # @private
  # @static
  # @see Model.connection

  ##
  # Schema for this model.
  # Maps from column path to property object
  # @property _schema
  # @type StringMap&lt;Object&gt;
  # @private
  # @static
  # @see Model.connection

  ##
  # Returns a new model class extending Model
  # @param {Connection} connection
  # @param {String} name
  # @param {Object} schema
  # @return {Class&lt;Model&gt;}
  @newModel: (connection, name, schema) -&gt;
    class NewModel extends Model
    NewModel.connection connection, name
    for name, property of schema
      NewModel.column name, property
    return NewModel

  ##
  # Sets a connection of this model
  #
  # If this methods was not called explicitly, this model will use Connection.defaultConnection
  # @param {Connection} connection
  # @param {String} [name]
  @connection: (connection, name) -&gt;
    if @hasOwnProperty '_connection'
      throw new Error 'Model::connection was called twice'

    name = @name if not name
    connection.models[name] = @
    connection[name] = @

    Object.defineProperty @, '_connection', value: connection
    Object.defineProperty @, '_adapter', value: connection._adapter
    Object.defineProperty @, '_associations', value: {}
    Object.defineProperty @, '_validators', value: []
    Object.defineProperty @, '_name', value: name
    Object.defineProperty @, '_schema_changed', writable: true, value: true
    Object.defineProperty @, '_schema', value: {}
    Object.defineProperty @, '_intermediate_paths', value: {}
    Object.defineProperty @, '_indexes', value: []
    Object.defineProperty @, '_integrities', value: []

  @_checkConnection: -&gt;
    return if @hasOwnProperty '_connection'
    if not Model._Connection.defaultConnection?
      throw new Error 'Create a Connection before creating a Model'
    @connection Model._Connection.defaultConnection

  @_waitingForReady: (object, method, args) -&gt;
    return true if @_connection._waitingForApplyingSchemas object, method, args
    return @_connection._waitingForConnection object, method, args

  @_getKeyType: (target_connection = @_connection) -&gt;
    if @_connection is target_connection and target_connection._adapter.key_type_internal
      target_connection._adapter.key_type_internal
    else
      target_connection._adapter.key_type

  ##
  # Adds a column to this model
  # @param {String} path
  # @param {Function|String|ColumnProperty} property
  @column: (path, property) -&gt;
    @_checkConnection()

    # nested path
    if typeof property is 'object' and not Array.isArray(property) and (not property.type or property.type.type)
      for subcolumn, subproperty of property
        @column path+'.'+subcolumn, subproperty
      return

    if @_schema.hasOwnProperty path
      # if using association, a column may be defined more than twice (by hasMany and belongsTo, for example)
      # overwrite some properties if given later
      if property?.required?
        @_schema[path].required = property.required
      return

    # convert simple type to property object
    if typeof property is 'function' or typeof property is 'string' or Array.isArray property
      property = type: property

    if Array.isArray property.type
      property.array = true
      property.type = property.type[0]

    type = types._toCORMOType property.type
    if type is types.RecordID
      type = @_getKeyType property.connection
      property.record_id = true

    # check supports of GeoPoint
    if type is types.GeoPoint and not @_adapter.support_geopoint
      throw new Error 'this adapter does not support GeoPoint'

    parts = path.split '.'
    for i in [0...parts.length-1]
      @_intermediate_paths[parts[0..i].join '.'] = 1

    property.type = type
    property._parts = path.split '.'
    property._dbname = path.replace '.', '_'

    @_schema[path] = property

    @_schema_changed = true
    @_connection._schema_changed = true

  ##
  # Adds an index to this model
  # @param {Object} columns hash of &lt;column, order&gt;
  # @param {Object} [options]
  # @param {Boolean} [options.unique]
  @index: (columns, options) -&gt;
    @_checkConnection()

    options ||= {}
    if not options.name
      options.name = Object.keys(columns).join('_')
    @_indexes.push columns: columns, options: options

  ##
  # Drops this model from the database
  # @param {Function} callback
  # @param {Error} callback.error
  # @see AdapterBase::drop
  @drop: (callback) -&gt;
    # do not need to apply schema before drop, only waiting connection established
    return if @_connection._waitingForConnection @, @drop, arguments

    @_adapter.drop @_name, _bindDomain (error) =&gt;
      @_schema_changed = true
      @_connection._schema_changed = true
      callback error

  ##
  # Creates a record.
  # 'Model.build(data)' is the same as 'new Model(data)'
  # @param {Object} [data={}]
  # @return {Model}
  @build: (data) -&gt;
    return new @ data

  ##
  # @property _prev_attributes
  # @private

  ##
  # @property _attributes
  # @private

  ##
  # @property _intermediates
  # @private

  ##
  # Creates a record
  # @param {Object} [data={}]
  constructor: (data) -&gt;
    data = data or {}
    ctor = @constructor
    schema = ctor._schema
    adapter = ctor._adapter
    dont_eliminate_null = not ctor.eliminate_null

    Object.defineProperty @, '_prev_attributes', writable: true, value: {}
    if ctor.dirty_tracking
      Object.defineProperty @, '_attributes', value: {}
      Object.defineProperty @, '_intermediates', value: {}
      for path in Object.keys(ctor._intermediate_paths).sort()
        [obj, last] = util.getLeafOfPath @, path
        @_intermediates[path] = {}
        @_defineProperty obj, last, path, false
      for column, property of schema
        [obj, last] = util.getLeafOfPath @, property._parts
        @_defineProperty obj, last, column, false
    else
      Object.defineProperty @, 'isDirty', value: -&gt; true
      Object.defineProperty @, 'getChanged', value: -&gt; []
      Object.defineProperty @, 'get', value: (path) -&gt; util.getPropertyOfPath @, path.split '.'
      Object.defineProperty @, 'getPrevious', value: -&gt;
      Object.defineProperty @, 'set', value: (path, value) -&gt; util.setPropertyOfPath @, path.split('.'), value
      Object.defineProperty @, 'reset', value: -&gt;

    if id = arguments[1]
      # if id exists, this is called from adapter with database record data
      selected_columns = arguments[2]
      support_nested = adapter.support_nested
      for column, property of schema
        if selected_columns and selected_columns.indexOf(column) is -1
          continue
        parts = property._parts
        value = if support_nested
          util.getPropertyOfPath data, parts
        else
          data[property._dbname]
        if value?
          value = adapter.valueToModel value, column, property
        else
          value = null
        if value? or dont_eliminate_null
          util.setPropertyOfPath @, parts, value

      @_collapseNestedNulls selected_columns if dont_eliminate_null

      Object.defineProperty @, 'id', configurable: false, enumerable: true, writable: false, value: id

      @_runCallbacks 'find', 'after'
    else
      for column, property of schema
        parts = property._parts
        value = util.getPropertyOfPath data, parts
        if value is undefined
          value = null
        if value? or dont_eliminate_null
          util.setPropertyOfPath @, parts, value

      @_collapseNestedNulls() if dont_eliminate_null

      Object.defineProperty @, 'id', configurable: true, enumerable: true, writable: false, value: null

    @_runCallbacks 'initialize', 'after'

  ##
  # Set nested object null if all children are null
  _collapseNestedNulls: (selected_columns) -&gt;
    ctor = @constructor
    for path in Object.keys(ctor._intermediate_paths)
      if selected_columns and selected_columns.indexOf(path) is -1
        continue
      if ctor.dirty_tracking
        obj = @_intermediates
        last = path
      else
        [obj, last] = util.getLeafOfPath @, path
      has_non_null = false
      for key, value of obj[last]
        has_non_null = true if value?
      if not has_non_null
        obj[last] = null

  _defineProperty: (object, key, path, enumerable) -&gt;
    Object.defineProperty object, key,
      configurable: true
      enumerable: enumerable
      get: =&gt; @get path
      set: (value) =&gt; @set path, value

  ##
  # Returns true if there is some changed columns
  isDirty: -&gt;
    Object.keys(@_prev_attributes).length &gt; 0

  ##
  # Returns the list of paths of changed columns
  getChanged: -&gt;
    Object.keys @_prev_attributes

  ##
  # Returns the current value of the column of the given path
  # @param {String} path
  # @return {}
  get: (path) -&gt;
    if @_intermediates.hasOwnProperty path
      @_intermediates[path]
    else
      util.getPropertyOfPath @_attributes, path

  ##
  # Returns the original value of the column of the given path
  # @param {String} path
  # @return {}
  getPrevious: (path) -&gt;
    @_prev_attributes[path]

  ##
  # Changes the value of the column of the given path
  # @param {String} path
  # @param {} value
  # @return {}
  set: (path, value) -&gt;
    eliminate_null = @constructor.eliminate_null
    if @_intermediates.hasOwnProperty path
      obj = @_intermediates[path]
      for k of obj
        obj[k] = undefined
      for k, v of value
        obj[k] = v
    else
      parts = path.split '.'
      prev_value = util.getPropertyOfPath @_attributes, parts
      return if prev_value is value
      if not @_prev_attributes.hasOwnProperty path
        @_prev_attributes[path] = prev_value
      [obj, last] = util.getLeafOfPath @, parts
      if eliminate_null
        @_defineProperty obj, last, path, value? if eliminate_null or prev_value is undefined
      else if prev_value is undefined
        @_defineProperty obj, last, path, true
      util.setPropertyOfPath @_attributes, parts, value
      while parts.length &gt; 1
        parts.pop()
        [obj, last] = util.getLeafOfPath @, parts
        @_defineProperty obj, last, parts.join('.'), true

  ##
  # Resets all changes
  reset: -&gt;
    for path, value of @_prev_attributes
      @set path, value
    @_prev_attributes = {}

  ##
  # Destroys this record (remove from the database)
  # @param {Function} callback
  # @param {Error} callback.error
  destroy: (callback) -&gt;
    callback = (-&gt;) if typeof callback isnt 'function'
    @_runCallbacks 'destroy', 'before'
    if @id
      @constructor.delete { id: @id }, (error, count) =&gt;
        @_runCallbacks 'destroy', 'after'
        callback error
    else
      @_runCallbacks 'destroy', 'after'
      callback null
    return

  ##
  # Deletes all records from the database
  # @param {Function} callback
  # @param {Error} callback.error
  @deleteAll: (callback) -&gt;
    callback = (-&gt;) if typeof callback isnt 'function'
    @delete callback
    return

  ##
  # Adds a has-many association
  # @param {Class&lt;Model&gt;|String} target_model_or_column
  # @param {Object} [options]
  # @param {String} [options.type]
  # @param {String} [options.as]
  # @param {String} [options.foreign_key]
  # @param {String} [options.integrity='ignore'] 'ignore', 'nullify', 'restrict', or 'delete'
  @hasMany: (target_model_or_column, options) -&gt;
    @_checkConnection()

    @_connection.addAssociation
      type: 'hasMany'
      this_model: @
      target_model_or_column: target_model_or_column
      options: options

  ##
  # Adds a has-one association
  # @param {Class&lt;Model&gt;|String} target_model_or_column
  # @param {Object} [options]
  # @param {String} [options.type]
  # @param {String} [options.as]
  # @param {String} [options.foreign_key]
  # @param {String} [options.integrity='ignore'] 'ignore', 'nullify', 'restrict', or 'delete'
  @hasOne: (target_model_or_column, options) -&gt;
    @_checkConnection()

    @_connection.addAssociation
      type: 'hasOne'
      this_model: @
      target_model_or_column: target_model_or_column
      options: options

  ##
  # Adds a belongs-to association
  # @param {Class&lt;Model&gt;|String} target_model_or_column
  # @param {Object} [options]
  # @param {String} [options.type]
  # @param {String} [options.as]
  # @param {String} [options.foreign_key]
  # @param {Boolean} [options.required]
  @belongsTo: (target_model_or_column, options) -&gt;
    @_checkConnection()

    @_connection.addAssociation
      type: 'belongsTo'
      this_model: @
      target_model_or_column: target_model_or_column
      options: options

_use = (file) -&gt;
  MixClass = require &quot;./model/#{file}&quot;
  _.extend Model, MixClass
  _.extend Model::, MixClass::
_use 'query'
_use 'callback'
_use 'timestamp'
_use 'persistence'
_use 'validate'
_use 'cache'

module.exports = Model</pre></div></div></div><script src="http://code.jquery.com/jquery-1.8.2.min.js"></script><script src="../bootstrap/js/bootstrap.min.js"></script><script src="../google-code-prettify/prettify.js"></script><script>$(function() {
  // make result of marked for pretty print
  $('pre code[class^="lang-"]').addClass('.prettyprint');
  window.prettyPrint && prettyPrint()
});
$('body').on('click', '.showcode', function () {
  $(this).parent().find('pre.sourcecode').toggle();
  $('body').scrollspy('refresh');
});
$('#options-private').on('click', function () {
  $('.private').toggle();
  $('body').scrollspy('refresh');
});</script></body></html>