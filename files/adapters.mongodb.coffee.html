<!DOCTYPE html><html lang="en"><head><meta charset="utf-8"><meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no"><title>CORMO - adapters.mongodb.coffee</title><link href="../bootstrap/css/bootstrap.min.css" rel="stylesheet" type="text/css"><link href="../bootstrap/css/bootstrap-responsive.min.css" rel="stylesheet" type="text/css"><link href="../google-code-prettify/prettify.css" rel="stylesheet" type="text/css"><link href="../style.css" rel="stylesheet" type="text/css"></head><body data-spy="scroll" data-target=".sidebar"><div class="navbar navbar-inverse navbar-fixed-top"><div class="navbar-inner"><div class="container-fluid"><ul class="nav"><li class=""><a href="../index.html">Home</a></li><li class=""><a href="../modules/cormo.html">Modules</a></li><li class=""><a href="../classes/ColumnProperty.html">Classes</a></li><li class="dropdown active"><a data-toggle="dropdown" href="#" class="dropdown-toggle">Files - adapters.mongodb.coffee <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../files/connection.coffee.html">connection.coffee</a></li><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/inflector.coffee.html">inflector.coffee</a></li><li><a href="../files/model.coffee.html">model.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li><li><a href="../files/util.coffee.html">util.coffee</a></li><li><a href="../files/adapters.base.coffee.html">adapters.base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">adapters.mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">adapters.mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">adapters.postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">adapters.redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">adapters.sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">adapters.sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">adapters.sqlite3_memory.coffee</a></li><li><a href="../files/connection.association.coffee.html">connection.association.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">connection.manipulate.coffee</a></li><li><a href="../files/model.callback.coffee.html">model.callback.coffee</a></li><li><a href="../files/model.persistence.coffee.html">model.persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">model.query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">model.timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">model.validate.coffee</a></li></ul></li></ul><div class="options"><label class="checkbox"><input id="options-private" type="checkbox"> Private </label></div></div></div></div><div class="container-fluid content"><div class="row-fluid"><div class="span3 visible-desktop"><div data-spy="affix" class="span3 sidebar"><ul class="nav nav-list cormo-sidenav"><li><a href="../files/connection.coffee.html">connection.coffee</a></li><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/inflector.coffee.html">inflector.coffee</a></li><li><a href="../files/model.coffee.html">model.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li><li><a href="../files/util.coffee.html">util.coffee</a></li><li class="nav-header">adapters</li><li><a href="../files/adapters.base.coffee.html">base.coffee</a></li><li class="active"><a href="../files/adapters.mongodb.coffee.html">mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">sqlite3_memory.coffee</a></li><li class="nav-header">connection</li><li><a href="../files/connection.association.coffee.html">association.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">manipulate.coffee</a></li><li class="nav-header">model</li><li><a href="../files/model.callback.coffee.html">callback.coffee</a></li><li><a href="../files/model.persistence.coffee.html">persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">validate.coffee</a></li></ul></div></div><div class="span9"><section><h1>adapters.mongodb.coffee</h1></section><pre class="prettyprint">try
  mongodb = require 'mongodb'
catch error
  console.log 'Install mongodb module to use this adapter'
  process.exit 1

ObjectID = mongodb.ObjectID

AdapterBase = require './base'
types = require '../types'
tableize = require('../inflector').tableize
async = require 'async'

_convertValueToObjectID = (value, key) -&gt;
  try
    new ObjectID value
  catch e
    throw new Error(&quot;'#{key}' is not a valid id&quot;)

_buildWhereSingle = (property, key, value, not_op) -&gt;
  if key isnt 'id' and not property?
    throw new Error(&quot;unknown column '#{key}'&quot;)
  property_type = property?.type
  is_objectid = key is 'id' or property_type is 'objectid'
  if Array.isArray value
    if is_objectid
      value = value.map (v) -&gt; _convertValueToObjectID v, key
    if not_op
      value = $nin: value
    else
      value = $in: value
  else if typeof value is 'object' and (keys = Object.keys value).length is 1
    sub_key = keys[0]
    switch sub_key
      when '$not'
        return _buildWhereSingle property, key, value[sub_key], not not_op
      when '$gt', '$lt', '$gte', '$lte'
        sub_value = value[sub_key]
        sub_value = new Date sub_value if property_type is types.Date
        value = {}
        value[sub_key] = sub_value
        if not_op
          value = $not: value
        obj = {}
        obj[key] = value
        return obj
      when '$contains'
        value = new RegExp value[sub_key], 'i'
      when '$in'
        if is_objectid
          value[sub_key] = value[sub_key].map (v) -&gt; _convertValueToObjectID v, key
      else
        throw new Error &quot;unknown operator '#{sub_key}'&quot;
    if not_op
      value = $not: value
  else
    if is_objectid
      value = _convertValueToObjectID value, key
    if not_op
      value = $ne: value

  obj = {}
  key = '_id' if key is 'id'
  value = new Date value if property_type is types.Date
  obj[key] = value
  return obj

_buildWhere = (schema, conditions, conjunction='$and') -&gt;
  if Array.isArray conditions
    subs = conditions.map (condition) -&gt; _buildWhere schema, condition
  else if typeof conditions is 'object'
    keys = Object.keys conditions
    if keys.length is 0
      return
    else if keys.length is 1
      key = keys[0]
      if key.substr(0, 1) is '$'
        switch key
          when '$and'
            return _buildWhere schema, conditions[key], '$and'
          when '$or'
            return _buildWhere schema, conditions[key], '$or'
        return
      else
        return _buildWhereSingle schema[key], key, conditions[key]
    else
      subs = keys.map (key) -&gt; _buildWhereSingle schema[key], key, conditions[key]
  else
    throw new Error &quot;'#{JSON.stringify conditions}' is not an object&quot;

  if subs.length is 0
    return
  else if subs.length is 1
    return subs[0]
  else
    obj = {}
    obj[conjunction] = subs
    return obj

##
# Adapter for MongoDB
# @namespace adapter
class MongoDBAdapter extends AdapterBase
  key_type: types.String
  key_type_internal: 'objectid'
  support_geopoint: true
  support_nested: true

  ##
  # Creates a MongoDB adapter
  constructor: (connection) -&gt;
    @_connection = connection
    @_collections = {}

  _collection: (name) -&gt;
    name = tableize name
    if not @_collections[name]
      return @_collections[name] = new mongodb.Collection @_client, name
    else
      return @_collections[name]

  ## @override AdapterBase::applySchema
  applySchema: (model, callback) -&gt;
    collection = @_collection(model)
    indexes = []
    for column, property of @_connection.models[model]._schema
      if property.unique
        if property.required
          indexes.push [ column, { unique: true } ]
        else
          indexes.push [ column, { unique: true, sparse: true } ]
      if property.type is types.GeoPoint
        obj = {}
        obj[column] = '2d'
        indexes.push [ obj ]
    for index in @_connection.models[model]._indexes
      if index.options.unique
        indexes.push [ index.columns, { name: index.options.name, unique: true, sparse: true } ]
      else
        indexes.push [ index.columns, { name: index.options.name } ]
    async.forEach indexes, (index, callback) -&gt;
        collection.ensureIndex index[0], index[1], (error) -&gt;
          callback error
      , (error) -&gt;
        callback error

  ## @override AdapterBase::drop
  drop: (model, callback) -&gt;
    name = tableize model
    delete @_collections[name]
    @_client.dropCollection name, (error) -&gt;
      # ignore not found error
      if error and error.errmsg isnt 'ns not found'
        return callback MongoDBAdapter.wrapError 'unknown error', error
      callback null

  idToDB: (value) -&gt;
    try
      return new ObjectID value
    catch e
      throw new Error(&quot;'id' is not a valid ID&quot;)

  valueToDB: (value, column, property) -&gt;
    return if not value?
    # convert id type
    if column is 'id' or property.type is 'objectid'
      try
        return new ObjectID value
      catch e
        throw new Error(&quot;'#{column}' is not a valid ID&quot;)
    return value

  _getModelID: (data) -&gt;
    data._id.toString()

  valueToModel: (value, column, property) -&gt;
    if property.type is 'objectid'
      value.toString()
    else
      value

  ## @override AdapterBase::create
  create: (model, data, callback) -&gt;
    @_collection(model).insert data, safe: true, (error, result) -&gt;
      if error?.code is 11000
        column = ''
        key = error.err.match /index: [\w-.]+\$(\w+)/
        if key?
          column = key[1]
          key = column.match /(\w+)_1/
          if key?
            column = key[1]
          column = ' ' + column
        return callback new Error('duplicated' + column)
      return callback MongoDBAdapter.wrapError 'unknown error', error if error
      id = result?[0]?._id.toString()
      if id
        delete data._id
        callback null, id
      else
        callback new Error 'unexpected result'

  ## @override AdapterBase::create
  createBulk: (model, data, callback) -&gt;
    @_collection(model).insert data, safe: true, (error, result) -&gt;
      if error?.code is 11000
        key = error.err.match /index: [\w-.]+\$(\w+)_1/
        return callback new Error('duplicated ' + key?[1])
      return callback MongoDBAdapter.wrapError 'unknown error', error if error
      error = undefined
      ids = result.map (doc) -&gt;
        id = doc._id.toString()
        if id
          delete data._id
        else
          error = new Error 'unexpected result'
        return id
      return callback error if error
      callback null, ids

  ## @override AdapterBase::update
  update: (model, data, callback) -&gt;
    id = data.id
    delete data.id
    @_collection(model).update { _id: id }, data, safe: true, (error) -&gt;
      if error?.code is 11001
        key = error.err.match /index: [\w-.]+\$(\w+)_1/
        return callback new Error('duplicated ' + key?[1])
      return callback MongoDBAdapter.wrapError 'unknown error', error if error
      callback null

  _buildUpdateOps: (schema, update_ops, data, path, object) -&gt;
    for column, value of object
      property = schema[path+column]
      if property
        if value?
          update_ops.$set[path+column] = value
        else
          update_ops.$unset[path+column] = ''
      else if typeof object[column] is 'object'
        @_buildUpdateOps schema, update_ops, data, path + column + '.', object[column]

  ## @override AdapterBase::updatePartial
  updatePartial: (model, data, conditions, options, callback) -&gt;
    schema = @_connection.models[model]._schema
    try
      conditions = _buildWhere schema, conditions
    catch e
      return callback e
    if not conditions
      conditions = {}
    update_ops = $set: {}, $unset: {}
    @_buildUpdateOps schema, update_ops, data, '', data
    @_collection(model).update conditions, update_ops, safe: true, multi: true, (error, count) -&gt;
      if error?.code is 11001
        key = error.err.match /index: [\w-.]+\$(\w+)_1/
        return callback new Error('duplicated ' + key?[1])
      return callback MongoDBAdapter.wrapError 'unknown error', error if error
      callback null, count

  ## @override AdapterBase::findById
  findById: (model, id, options, callback) -&gt;
    if options.select
      fields = {}
      options.select.forEach (column) -&gt; fields[column] = 1
    try
      id = new ObjectID id
    catch e
      return callback new Error('not found')
    client_options = {}
    if fields
      client_options.fields = fields
    @_collection(model).findOne _id: id, client_options, (error, result) =&gt;
      return callback MongoDBAdapter.wrapError 'unknown error', error if error
      return callback new Error('not found') if not result
      callback null, @_convertToModelInstance model, result, options.select

  ## @override AdapterBase::find
  find: (model, conditions, options, callback) -&gt;
    if options.select
      fields = {}
      options.select.forEach (column) -&gt; fields[column] = 1
    try
      conditions = _buildWhere @_connection.models[model]._schema, conditions
    catch e
      return callback e
    if options.near? and field = Object.keys(options.near)[0]
      obj = {}
      obj[field] = { $near: options.near[field] }
      if conditions
        conditions = { $and : [  conditions, obj ] }
      else
        conditions = obj
    if options.orders.length &gt; 0
      orders = {}
      options.orders.forEach (order) -&gt;
        if order[0] is '-'
          orders[order[1..]] = -1
        else
          orders[order] = 1
    #console.log JSON.stringify conditions
    client_options =
      limit: options.limit
    if fields
      client_options.fields = fields
    if orders
      client_options.sort = orders
    @_collection(model).find conditions, client_options, (error, cursor) =&gt;
      return callback MongoDBAdapter.wrapError 'unknown error', error if error or not cursor
      cursor.toArray (error, result) =&gt;
        return callback MongoDBAdapter.wrapError 'unknown error', error if error or not cursor
        callback null, result.map (record) =&gt; @_convertToModelInstance model, record, options.select

  ## @override AdapterBase::count
  count: (model, conditions, callback) -&gt;
    try
      conditions = _buildWhere @_connection.models[model]._schema, conditions
    catch e
      return callback e
    #console.log JSON.stringify conditions
    @_collection(model).count conditions, (error, count) =&gt;
      return callback MongoDBAdapter.wrapError 'unknown error', error if error
      callback null, count

  ## @override AdapterBase::delete
  delete: (model, conditions, callback) -&gt;
    try
      conditions = _buildWhere @_connection.models[model]._schema, conditions
    catch e
      return callback e
    #console.log JSON.stringify conditions
    @_collection(model).remove conditions, safe: true, (error, count) -&gt;
      return callback MongoDBAdapter.wrapError 'unknown error', error if error
      callback null, count

  ##
  # Connects to the database
  # @param {Object} settings
  # @param {String} [settings.host='localhost']
  # @param {Number} [settings.port=27017]
  # @param {String} [settings.user]
  # @param {String} [settings.password]
  # @param {String} settings.database
  # @param {Function} callback
  # @param {Error} callback.error
  connect: (settings, callback) -&gt;
    server = new mongodb.Server settings.host or 'localhost', settings.port or 27017, {}
    db = new mongodb.Db settings.database, server, safe: true
    db.open (error, client) =&gt;
      return callback MongoDBAdapter.wrapError 'unknown error', error if error
      if settings.user or settings.password
        db.authenticate settings.user, settings.password, (error, success) =&gt;
          if success
            @_client = client
            callback null
          else
            callback MongoDBAdapter.wrapError 'unknown error', error
      else
        @_client = client
        callback null

module.exports = (connection) -&gt;
  new MongoDBAdapter connection</pre></div></div></div><script src="http://code.jquery.com/jquery-1.8.2.min.js"></script><script src="../bootstrap/js/bootstrap.min.js"></script><script src="../google-code-prettify/prettify.js"></script><script>$(function() {
  // make result of marked for pretty print
  $('pre code[class^="lang-"]').addClass('.prettyprint');
  window.prettyPrint && prettyPrint()
});
$('body').on('click', '.showcode', function () {
  $(this).parent().find('pre.sourcecode').toggle();
  $('body').scrollspy('refresh');
});
$('#options-private').on('click', function () {
  $('.private').toggle();
  $('body').scrollspy('refresh');
});</script></body></html>